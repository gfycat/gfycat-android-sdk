/**
 * Here you may define you PC local way to obtain passwords.
 */
ext {

    def localPropertiesFile = project.rootProject.file('local.properties');
    Properties localProperties = new Properties()
    if (localPropertiesFile.exists()) {
        localProperties.load(localPropertiesFile.newDataInputStream())
    }

    /**
     * We will look required variable in local.properties, system environment and in command line properties.
     */
    getValue = { name ->
        def result = localProperties.getProperty(name)

        if (result == null) result = System.getenv(name)

        if (result == null && getRootProject().hasProperty(name)) result = getRootProject().getProperty(name)

        if (result == null) result = "NO_SUCH_PROPERTY"

        return result
    }

    /**
     * bintray(jcenter) variables.
     */
    getGfycatBintrayUser = {
        return getValue('GFYCAT_BINTRAY_USER')
    }
    getGfycatBintrayApiKey = {
        return getValue('GFYCAT_BINTRAY_API_KEY')
    }
}
